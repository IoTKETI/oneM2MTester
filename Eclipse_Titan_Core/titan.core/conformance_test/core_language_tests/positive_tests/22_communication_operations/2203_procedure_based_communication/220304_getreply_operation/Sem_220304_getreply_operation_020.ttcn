/******************************************************************************
 * Copyright (C) 2017 ETSI  All Rights Reserved.
 *
 * Adrien Kirjak
 *
 ** @version  0.0.1
 ** @purpose  1:22.3.4, @decoded redirect value assignment with encoding parameter
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/

// The following requirements are tested:
// In case the referenced field is of the universal charstring type, the @decoded 
// clause can contain an optional parameter defining the encoding format. The 
// parameter shall be of the charstring type and it shall contain one of the 
// strings allowed for the decvalue_unichar function (specified in clause C.5.4).

module Sem_220304_getreply_operation_020 {
	type record R {
        integer id,
        universal charstring payload
    }

	type integer I with {variant "32 bit"} 
    
    signature S() return R;
    
	type port P procedure {
		inout S;
	} with {extension "internal"}
	
    type component GeneralComp {
		port P p;
	}
    
    function f_server() runs on GeneralComp {
        var I v_src := 1953719668;
        var R v_rec := { id := 5, payload := encvalue_unichar(v_src, "UTF-16LE") };
        p.getcall(S: {});
        p.reply(S:{} value v_rec);
    }
	
    testcase TC_Sem_220304_getreply_operation_020() runs on GeneralComp system GeneralComp {
        var I v_res;        
        var GeneralComp v_ptc := GeneralComp.create("PTC");
        connect(self:p, v_ptc:p);
        v_ptc.start(f_server());

        p.call(S:{}) {
            [] p.getreply(S: {} value R:?) -> value (v_res := @decoded("UTF-16LE") payload) { 
                if (v_res == 1953719668) { setverdict (pass); }
                else { setverdict(fail); }
            }
            [] p.getreply { setverdict(fail); }
        };
        v_ptc.done;
        setverdict(pass);
    }

    control {
        execute(TC_Sem_220304_getreply_operation_020(), 5.0);
    }
} with {encode "RAW"}
