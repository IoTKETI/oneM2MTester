/******************************************************************************
 * Copyright (C) 2017 ETSI  All Rights Reserved.
 *
 * Adrien Kirjak
 *
 ** @version  0.0.1
 ** @purpose  9:7.1.6, check correct namespace prefix encoding for attributeFormDefault
 ** @verdict  pass reject
 ***************************************************/
module Pos_070106_form_012 {

    import from schema_Pos_070106_form_012 language "XSD" all;

    template MyElement m_msg := {
      myField := 1,
      myField2 := {2},
      myElement := {3}
    }

	external function enc_MyType(in MyElement pdu) return octetstring
  	 with { extension "prototype (convert) encode(XER:XER_EXTENDED)" }
  
  	external function dec_MyType(in octetstring stream) return MyElement
  	 with { extension "prototype (convert) decode(XER:XER_EXTENDED)" }

    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type port P message {
        inout Raw;
    } with {extension "internal"}

    type component C {
        port P p;
		port P p1;
    }

    testcase TC_Pos_070106_form_012() runs on C system C {

        var Raw v_rcv;

        var Raw  v_Raw:="<ns233_1:MyElement xmlns:ns233_1='schema:Pos_070106_form_012' xmlns:ns233_2='schema:Pos_070106_form_012_2' myField='1'>\n\t<myField2 ns233_2:myField='2'/>\n\t<ns233_2:MyElement ns233_2:myField='3'/>\n</ns233_1:MyElement>\n\n";

        connect(self:p, self:p1);

		p.send(oct2unichar(enc_MyType(valueof(m_msg))));

		alt {
            // compare the encoded message with the reference XML file
            [] p1.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (match(v_rcv, v_Raw) ) {
					alt {
                     	[] p1.receive(Raw:?) {
							if(match(dec_MyType(unichar2oct(v_rcv)),m_msg))	{
								setverdict(pass, "Decoded value matches encoded template and reference XML");
							}
							else 
								{setverdict(fail, "XML decoding failure, reason:", dec_MyType(unichar2oct(v_rcv)));}	
						}
					}
                } else {
                    setverdict(fail, "Unexpected message: ", v_rcv, " expected: ", v_Raw);
                }
            }
            [] p1.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_070106_form_012(), PX_TC_EXECUTION_TIMEOUT);
    }

}
