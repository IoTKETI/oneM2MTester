/******************************************************************************
 * Copyright (C) 2017 ETSI  All Rights Reserved.
 *
 * Adrien Kirjak
 *
 ** @version  0.0.1
 ** @purpose  9:7.7.2, Verify that the naming rules apply to converted anyAttribute field
 ** @verdict  pass accept, ttcn3verdict:pass
***************************************************/
// The following requirements are tested:
// The name of the field shall be the result of applying clause 5.2.2 to "attr".

module Pos_070702_the_anyattribute_element_003 {

   import from schema_Pos_070702_the_anyattribute_element_003 language "XSD" all;

   template MyType m_msg := {
       attr := "test",
       attr_1 := {
           "schema:Pos_070702_the_anyattribute_element_003 bar=""bar""",
           "schema:Pos_070702_the_anyattribute_element_003 foo=""foo"""
       },
       base := "Hello world!"
   };

	external function enc_MyType(in MyType pdu) return octetstring
  	 with { extension "prototype (convert) encode(XER:XER_EXTENDED)" }
  
  	external function dec_MyType(in octetstring stream) return MyType
  	 with { extension "prototype (convert) decode(XER:XER_EXTENDED)" }

    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type port P message {
        inout Raw;
    } with {extension "internal"}

    type component C {
        port P p;
		port P p1;
    }

    testcase TC_Pos_070702_the_anyattribute_element_003() runs on C system C {
        var Raw v_rcv;

        var Raw  v_Raw:= "<ns217:MyType xmlns:ns217='schema:Pos_070702_the_anyattribute_element_003' attr='test' xmlns:b0='schema:Pos_070702_the_anyattribute_element_003' b0:bar=\"bar\" xmlns:b1='schema:Pos_070702_the_anyattribute_element_003' b1:foo=\"foo\">Hello world!</ns217:MyType>\n\n";

        connect(self:p, self:p1);

		p.send(oct2unichar(enc_MyType(valueof(m_msg))));

        alt {
            // compare the encoded message with the reference XML file
            [] p1.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (match(v_rcv, v_Raw) ) {
					alt {
                     	[] p1.receive(Raw:?) {
							if(match(dec_MyType(unichar2oct(v_rcv)),m_msg))	{
								setverdict(pass, "Decoded value matches encoded template and reference XML");
							}
							else 
								{setverdict(fail, "XML decoding failure, reason:", dec_MyType(unichar2oct(v_rcv)));}	
						}
					}
                } else {
                    setverdict(fail, "Unexpected message: ", v_rcv, " expected: ", v_Raw);
                }
            }
            [] p1.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_070702_the_anyattribute_element_003(), PX_TC_EXECUTION_TIMEOUT);
    }


}
