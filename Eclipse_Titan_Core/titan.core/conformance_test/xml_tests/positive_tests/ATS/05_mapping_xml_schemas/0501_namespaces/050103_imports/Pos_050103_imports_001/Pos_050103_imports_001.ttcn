/******************************************************************************
 * Copyright (C) 2017 ETSI  All Rights Reserved.
 *
 * Adrien Kirjak
 *
 ** @version  0.0.1
 ** @purpose  9:5.1.3, Verify that XSD import statement is handled correctly
 ** @verdict  pass accept, ttcn3verdict:pass
***************************************************/
// The following requirements are tested:
// All XSD import statements (i.e. import element information items and the related xmlns attributes, 
// where present) shall be mapped to equivalent TTCN-3 import statements, importing all definitions 
// from the other TTCN-3 module. All XSD components are public by default (see clause 8.2.3 of 
// ES 201 873-1 [1]). 

// Note: It is not possible to verify presence of the import clause in the generated code directly
// since XSD import statements cannot be imported. This test only tests if XSD schema containing
// an import element is processed correctly.

module Pos_050103_imports_001 {

   import from schema_Pos_050103_imports_001 language "XSD" all;

   template MyType m_msg := 1;

	external function enc_MyType(in MyType pdu) return octetstring
  	 with { extension "prototype (convert) encode(XER:XER_EXTENDED)" }
  
  	external function dec_MyType(in octetstring stream) return MyType
  	 with { extension "prototype (convert) decode(XER:XER_EXTENDED)" }

    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type port P message {
        inout MyType, Raw;
    } with {extension "internal"}

    type component C {
        port P p;
		port P p1;
    }

    testcase TC_Pos_050103_imports_001() runs on C system C {
        var Raw v_rcv;

		var Raw  v_Raw:="<ns007_1:MyType xmlns:ns007_1='schema:Pos_050103_imports_001'>1</ns007_1:MyType>\n";

        connect(self:p, self:p1);

        // encode the message
        p.send(oct2unichar(enc_MyType(valueof(m_msg))));

        alt {
            // compare the encoded message with the reference XML file
            [] p1.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (match(v_rcv, v_Raw) ) {
					alt {
                     	[] p1.receive(Raw:?) {
							if(match(dec_MyType(unichar2oct(v_rcv)),m_msg))	{
								setverdict(pass, "Decoded value matches encoded template and reference XML");
							}
							else 
							{	setverdict(fail, "XML decoding failure, reason:", dec_MyType(unichar2oct(v_rcv)));}	
						}
					}
                } else {
                    setverdict(fail, "Unexpected message: ", v_rcv, " expected: ", v_Raw);
                }
            }
            [] p1.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_050103_imports_001(), PX_TC_EXECUTION_TIMEOUT);
    }


}
