/******************************************************************************
 * Copyright (C) 2017 ETSI  All Rights Reserved.
 *
 * Adrien Kirjak
 *
 ** @version  0.0.1
 ** @purpose  9:5.1.4, Verify that qualified default element form is correctly processed (no namespace prefix)
 ** @verdict  pass accept, ttcn3verdict:pass
***************************************************/
// The following requirements are tested:
// If the TTCN-3 module corresponds to a (present) target namespace and the value of the ... 
// elementFormDefault attributes of any schema element information items that contribute to 
// the given TTCN-3 module is qualified, the encoding instructions ... "elementFormQualified" 
// shall be attached accordingly to the given TTCN-3 module.
//
// Note: as only end-to-end solution is tested, the presence of encoding attributes is not
// checked directly, but through the encoded messages.

module Pos_050104_attributes_of_the_xsd_schema_element_001 {

   import from schema_Pos_050104_attributes_of_the_xsd_schema_element_001 language "XSD" all;

   template MyType m_msg := 
   {
      item1 := "abc",
      item2 := "def"
   };

	external function enc_MyType(in MyType pdu) return octetstring
  	 with { extension "prototype (convert) encode(XER:XER_EXTENDED)" }
  
  	external function dec_MyType(in octetstring stream) return MyType
  	 with { extension "prototype (convert) decode(XER:XER_EXTENDED)" }

    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type port P message {
        inout MyType, Raw;
    } with {extension "internal"}

    type component C {
        port P p;
		port P p1;
    }

    testcase TC_Pos_050104_attributes_of_the_xsd_schema_element_001() runs on C system C {
        var Raw v_rcv;

		var Raw  v_Raw:="<MyType xmlns='schema:Pos_050104_attributes_of_the_xsd_schema_element_001'>\n\t<item1>abc</item1>\n\t<item2>def</item2>\n</MyType>\n\n";

        connect(self:p, self:p1);

        // encode the message
        p.send(oct2unichar(enc_MyType(valueof(m_msg))));

        alt {
            // compare the encoded message with the reference XML file
     		[] p1.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (match(v_rcv, v_Raw) ) {
					alt {
                     	[] p1.receive(Raw:?) {
							if(match(dec_MyType(unichar2oct(v_rcv)),m_msg))	{
								setverdict(pass, "Decoded value matches encoded template and reference XML");
							}
							else 
							{	setverdict(fail, "XML decoding failure, reason:", dec_MyType(unichar2oct(v_rcv)));}	
						}
					}
                } else {
                    setverdict(fail, "Unexpected message: ", v_rcv, " expected: ", v_Raw);
                }
            }
            [] p1.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_050104_attributes_of_the_xsd_schema_element_001(), PX_TC_EXECUTION_TIMEOUT);
    }


}
